buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:1.4'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
}

def webProjects() {
    subprojects.findAll { subproject -> subproject.plugins.hasPlugin('war') }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    configure(webProjects()) {
        apply plugin: 'cargo'

        cargo {
            containerId = 'glassfish4x'

            local {
                installer {
                    installUrl = 'http://download.java.net/glassfish/4.0/release/glassfish-4.0.zip'
                    downloadDir = file("$buildDir/download")
                    extractDir = file("$buildDir/extract")
                }
            }
        }

        cargoRunLocal.dependsOn assemble
    }
}